grammar org.eclipse.sirius.expression.text.ExpressionText hidden(WS)

import "http://www.eclipse.org/sirius/expression"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

SiriusExpressionPackage:
	'package' ePackage=[ecore::EPackage] '{'
		(expressionClasses+=SiriusExpressionClass)*
	'}'
;

SiriusExpressionClass:
	'class' eClass=[ecore::EClass|FQN] '{'
		(variables+=SiriusVariable)*
		(expressionDescriptions+=SiriusExpressionDescription)*
	'}'
;

FQN: ID ("." ID)*;

SiriusVariable:
	(documentation=ML_COMMENT)?
	'var' name=ID ':' eType=[ecore::EClassifier|FQN]
;

SiriusExpressionDescription:
	'exp' expression=[ecore::EAttribute|FQN] '(' (parameters+=SiriusParameter(',' parameters+=SiriusParameter)*)? ')' ':' returnType=[ecore::EClassifier|FQN] '[' lowerBound=Bound '..' upperBound=Bound  ']' '{'
		'userDefinedVariableContainers' '=' '[' 
			(variableContainers+=[ecore::EClass|FQN](',' variableContainers+=[ecore::EClass|FQN])*)?
		']'
	'}'
;

SiriusParameter:
	(optional?='optional')? variable=[SiriusVariable]
;

Bound returns ecore::EInt:
	('-')?INT
;

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ML_COMMENT	: '/*' -> '*/';
terminal WS			: (' '|'\t'|'\r'|'\n')+;